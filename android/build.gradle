
def DEFAULT_COMPILE_SDK_VERSION = 28
def DEFAULT_BUILD_TOOLS_VERSION = "28.0.3"
def DEFAULT_MIN_SDK_VERSION = 21
def DEFAULT_TARGET_SDK_VERSION = 28
def DEFAULT_ANDRIODX_CORE_VERSION = "1.6.0"
def DEFAULT_ANDROIDX_RECYCLERVIEW_VERSION = "1.2.1"
def DEFAULT_ANDROID_MATERIAL_VERSION = "1.4.0"
def DEFAULT_KOTLIN_VERSION = "1.5.0"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    compileSdkVersion safeExtGet("compileSdkVersion", DEFAULT_COMPILE_SDK_VERSION)
    buildToolsVersion safeExtGet("buildToolsVersion", DEFAULT_BUILD_TOOLS_VERSION)
    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", DEFAULT_MIN_SDK_VERSION)
        targetSdkVersion safeExtGet("targetSdkVersion", DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "$rootDir/../node_modules/react-native/android"
    }
    maven {
        url "$rootDir/../node_modules/jsc-android/dist"
    }
    google()
}

dependencies {
    implementation "com.facebook.react:react-native:+"
    implementation "androidx.core:core-ktx:${safeExtGet("androidXCore", DEFAULT_ANDRIODX_CORE_VERSION)}"
    implementation "androidx.recyclerview:recyclerview:${safeExtGet("androidXRecyclerview", DEFAULT_ANDROIDX_RECYCLERVIEW_VERSION)}"
    implementation "com.google.android.material:material:${safeExtGet("androidMaterial", DEFAULT_ANDROID_MATERIAL_VERSION)}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${safeExtGet("kotlin_version", DEFAULT_KOTLIN_VERSION)}"
}